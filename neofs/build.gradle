plugins {
    id "de.undercouch.download" version "4.1.2"
}

description 'NeoFS Java API'

repositories {
    mavenLocal()
}

dependencies {
    compile project(":core")

    compile "io.neow3j:neofs-api-java:$neofsApiJavaVersion",
            "com.google.protobuf:protobuf-java-util:$protobufJavaUtilVersion",
            "io.grpc:grpc-protobuf:$grpcProtobufVersion"

    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "net.java.dev.jna:jna-platform:$jnaVersion"
}

configurations {
    testArtifacts.extendsFrom testRuntime
}

artifacts {
    testArtifacts testJar
}

jar {
    from sourceSets.main.output.resourcesDir
}

// Todo: build task of this module should depend on the download tasks below
// Todo: Include all the .so files inside the jar (might be automatically done, but need to check)

task downloadLibZip(type: Download) {
    src "https://github.com/neow3j/neofs-api-shared-lib/releases/download/$neofsSharedLibVersion/libneofs-all.zip"
    dest new File(temporaryDir, 'libneofs-all.zip')
    onlyIfModified true
}

task downloadAndUnzipLib(dependsOn: downloadLibZip, type: Copy) {
    from zipTree(downloadLibZip.dest)
    into sourceSets.main.output.resourcesDir
}

build {
    dependsOn downloadAndUnzipLib
}

bundleJar {
    dependsOn downloadAndUnzipLib
}
